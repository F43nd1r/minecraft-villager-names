buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}
plugins {
    id 'maven-publish'
    id 'com.faendir.gradle.release' version '3.3.1'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'


sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.disableRefMap', 'true'
            arg "-mixin.config=villagernames.mixins.json"

            mods {
                villagerNamesMod {
                    source sourceSets.main
                }
            }
            jvmArgs "-Xmx4096M"
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.disableRefMap', 'true'
            arg "-mixin.config=villagernames.mixins.json"

            mods {
                villagerNamesMod {
                    source sourceSets.main
                }
            }
            jvmArgs "-Xmx2048M"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.4-35.0.15'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "villager-names",
            "Specification-Vendor": "f43nd1r",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${project.version}",
            "Implementation-Vendor" :"f43nd1r",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "villagernames.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

release {
    tagTemplate = "v\$version"
    ignoredSnapshotDependencies += "org.spongepowered:mixingradle"
}

mixin {
    add sourceSets.main, 'villagernames.mixins.refmap.json'
}